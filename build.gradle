buildscript {
  repositories {
    jcenter()
    maven { url "https://jitpack.io" }
  }
  dependencies {
    classpath 'ch.raffael.markdown-doclet:markdown-doclet:1.4'
    classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.16.0'
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    classpath 'com.github.mechjacktv:gradle-test-framework:master-SNAPSHOT'
  }
}

subprojects {
  apply plugin: 'ch.raffael.markdown-doclet'
  apply plugin: 'com.diffplug.gradle.spotless'
  apply plugin: 'com.google.protobuf'
  apply plugin: 'idea'
  apply plugin: 'jacoco'
  apply plugin: 'java'
  apply plugin: 'tv.mechjack.gradle.testframework'

  archivesBaseName = "${rootProject.name}-${project.name}"
  group = 'tv.mechjack'
  version = System.getenv("BUILDVER") != null ? System.getenv("BUILDVER") : "DEVELOP+${System.getProperty("user.name")}.${System.currentTimeMillis()}"

  jacocoTestReport {
    afterEvaluate {
      classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: '**/ProtoMessage**')
      }))
    }
  }
  check.dependsOn 'jacocoTestReport'

  spotless {
    java {
      importOrder 'java', 'javax', 'com', 'org', '', 'tv.mechjack'
      eclipse().configFile rootProject.file('src/buildscript/eclipse_java_formatter_rules.xml')
      removeUnusedImports()
      trimTrailingWhitespace()
      endWithNewline()
    }
    format 'misc', {
      target '**/*.gradle', '**/*.md', '**/.gitignore', '**/*.proto'
      trimTrailingWhitespace()
      indentWithSpaces(2)
      endWithNewline()
    }
  }
  compileJava.dependsOn 'spotlessApply'

  repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
  }
  dependencies {
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.+'
    implementation group: 'com.google.guava', name: 'guava', version: '23.+'
    implementation group: 'com.google.inject', name: 'guice', version: '4.+'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.+'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.+'
    testFrameworkImplementation group: 'com.github.mechjacktv', name: 'java-test-framework', version: 'develop-SNAPSHOT'
    testFrameworkImplementation group: 'junit', name: 'junit', version: '4.+'
    testFrameworkImplementation group: 'org.assertj', name: 'assertj-core', version: '3.+'
    testFrameworkImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.+'
    testRuntimeOnly group: 'org.slf4j', name: 'slf4j-nop', version: '1.+'
  }
}

/*******************************
 * BASE PROJECTS
 */

project(':platform') {
  archivesBaseName = "mechjack-${project.name}"

  dependencies {
    implementation group: 'org.mapdb', name: 'mapdb', version: '3.+'
  }
}

/*******************************
 * CORE PROJECT
 */

project(':base') {
  dependencies {
    implementation project(':platform')
    implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.+'
    implementation group: 'info.picocli', name: 'picocli', version: '3.8.2'
    implementation group: 'org.kitteh.irc', name: 'client-lib', version: '5.+'
    testFrameworkImplementation project(path: ':platform', configuration: 'testFramework')
  }
}

/*******************************
 * FEATURE PROJECTS
 */

project(':feature:core') {
  dependencies {
    implementation project(':base')
    implementation project(':platform')
    implementation group: 'info.picocli', name: 'picocli', version: '3.8.2'
    testFrameworkImplementation project(path: ':base', configuration: 'testFramework')
    testFrameworkImplementation project(path: ':platform', configuration: 'testFramework')
  }
}

project(':feature:customcommands') {
  dependencies {
    implementation project(':base')
    implementation project(':platform')
    implementation group: 'info.picocli', name: 'picocli', version: '3.8.2'
    testFrameworkImplementation project(path: ':base', configuration: 'testFramework')
    testFrameworkImplementation project(path: ':platform', configuration: 'testFramework')
  }
}

project(':feature:shoutouts') {
  dependencies {
    implementation project(':base')
    implementation project(':platform')
    implementation group: 'info.picocli', name: 'picocli', version: '3.8.2'
    testFrameworkImplementation project(path: ':base', configuration: 'testFramework')
    testFrameworkImplementation project(path: ':platform', configuration: 'testFramework')
  }
}

project(':feature:wouldyourather') {
  dependencies {
    implementation project(':base')
    implementation project(':platform')
    implementation group: 'info.picocli', name: 'picocli', version: '3.8.2'
    testFrameworkImplementation project(path: ':base', configuration: 'testFramework')
    testFrameworkImplementation project(path: ':platform', configuration: 'testFramework')
  }
}

/*******************************
 * APPLICATION WITH ALL PROJECTS
 */

project(':all') {
  apply plugin: 'application'

  mainClassName = 'tv.mechjack.mechjackbot.main.Main'

  dependencies {
    runtimeOnly project(':feature:core')
    runtimeOnly project(':feature:customcommands')
    runtimeOnly project(':feature:shoutouts')
    runtimeOnly project(':feature:wouldyourather')
    runtimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: '1.+'
  }

  distributions {
    main {
      baseName = "mechjackbot-${project.getProperty('name')}"
    }
  }
  startScripts {
    applicationName = 'mechjackbot'
  }
}
